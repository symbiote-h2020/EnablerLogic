buildscript {
    ext {
        springBootVersion = '1.5.4.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'java'
    id 'io.franzbecker.gradle-lombok' version '1.8'
    id 'org.springframework.boot' version '1.5.4.RELEASE'

    // code quality
    id "jacoco"

    // IDEs
    id "eclipse"
    id "idea"
    
    // publishing
    id "maven"
    id "maven-publish"
}

group = 'eu.h2020.symbiote'
version = '0.0.1-SNAPSHOT'

// this is needed for starter to not create fat jar
bootRepackage.enabled = false

jar {
    baseName = 'enabler-logic-starter'
    version =  project.version
}

// adding sources for integrationTests
sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}


//allprojects {
//	repositories {
//		jcenter()
//		maven { url "https://jitpack.io" }
//	}
//}

repositories {
	mavenLocal()
	mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
	compile('org.springframework.boot:spring-boot-starter')
    compile('org.springframework.cloud:spring-cloud-starter-zipkin') // Causes the java.net.ConnectException if Zipkin offline
	compile('org.springframework.boot:spring-boot-starter-amqp')
    compile('org.springframework.boot:spring-boot-starter-data-rest')
    compile('org.springframework.boot:spring-boot-starter-data-mongodb')
	compile('eu.h2020.symbiote:SymbIoTeLibraries:1.0.0-SNAPSHOT')
	//compile('com.github.symbiote-h2020:SymbIoTeLibraries:develop-SNAPSHOT') { changing = true }
	
	compileOnly('org.projectlombok:lombok')
	
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('io.arivera.oss:embedded-rabbitmq:1.1.2')
    testCompile('org.awaitility:awaitility:3.0.0')
    testCompile('com.revinate:assertj-json:1.0.1')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Dalston.SR2"
    }
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {
		mavenLocal()
	}
}

configurations.all {
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

// javadoc generation
task generateJavaDocs(type: Javadoc) {
    source = sourceSets.main.allJava
    destinationDir = reporting.file("javadocs")
}


// jacoco configuration section
jacoco {
    toolVersion = "0.7.9"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

// including code quality extensions into the build
check.dependsOn(jacocoTestReport)

// integration tests
task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

// reports for integration tests in different directory
tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}


/*
Run only unit tests
 > gradle clean test

Run only integration tests
> gradle clean integrationTest

Run all tests
> gradle clean test integrationTest

Run build with all tests
>  gradle clean build

Run build and exclude integration tests
> gradle clean build -x integrationTest

publishing starter to mavenLocal
> gradle clean build publishToMavenLocal
 */

